# Copyright (C) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/distributeddatamgr/relational_store/relational_store.gni")

subsystem_name = "distributeddatamgr"
part_name = "relational_store"
taihe_generated_file_path =
    "$taihe_file_path/out/$subsystem_name/$part_name/cloud_data"

copy_taihe_idl("cloud_data_taihe_idl") {
  sources = [
    "idl/ohos.data.cloudData.sharing.taihe",
    "idl/ohos.data.cloudData.taihe",
  ]
  external_deps = [
    "data_object:copy_taihe"
  ]
}

ohos_taihe("cloud_data_generate") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":cloud_data_taihe_idl" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.data.cloudData.ani.cpp",
    "$taihe_generated_file_path/src/ohos.data.cloudData.abi.c",
    "$taihe_generated_file_path/src/ohos.data.cloudData.sharing.ani.cpp",
    "$taihe_generated_file_path/src/ohos.data.cloudData.sharing.abi.c",
  ]
}

taihe_shared_library("cloud_data_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"
  include_dirs = [
    "${relational_store_frameworks_path}/common/include",
    "${relational_store_frameworks_path}/ets/taihe/cloud_data/include",
    "${relational_store_frameworks_path}/ets/taihe/relational_store/include",
    "${relational_store_frameworks_path}/js/napi/cloud_data/include",
    "${relational_store_innerapi_path}/cloud_data/include",
    "${relational_store_innerapi_path}/common_type/include",
    "${relational_store_innerapi_path}/rdb/include",
    "${relational_store_frameworks_path}/js/napi/common/include",
    "${relational_store_innerapi_path}/rdb/include",
    "${relational_store_frameworks_path}/js/napi/relationalstore/include",
  ]
  sources = get_target_outputs(":cloud_data_generate")
  sources += [
    "${relational_store_frameworks_path}/ets/taihe/cloud_data/src/ani_cloud_data_utils.cpp",
    "${relational_store_frameworks_path}/ets/taihe/cloud_data/src/ani_constructor.cpp",
    "${relational_store_frameworks_path}/ets/taihe/cloud_data/src/ani_error_code.cpp",
    "${relational_store_frameworks_path}/ets/taihe/cloud_data/src/ohos.data.cloudData.impl.cpp",
    "${relational_store_frameworks_path}/ets/taihe/cloud_data/src/ohos.data.cloudData.sharing.impl.cpp",
  ]
  deps = [
    ":cloud_data_generate",
    "${relational_store_frameworks_path}/ets/taihe/relational_store:relationalstore_ani",
    "${relational_store_innerapi_path}/cloud_data:cloud_data_native",
    "${relational_store_innerapi_path}/rdb:native_rdb",
    "${relational_store_innerapi_path}/rdb_data_share_adapter:rdb_data_share_adapter",
  ]

  sanitize = {
    boundary_sanitize = true
    ubsan = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  cflags = [
    "-DHILOG_ENABLE",
    "-fPIC",
    "-g3",
  ]
  external_deps = [
    "ability_runtime:abilitykit_native",
    "ability_runtime:ani_base_context",
    "ability_runtime:app_context",
    "ability_runtime:extensionkit_native",
    "ability_runtime:napi_base_context",
    "data_share:datashare_common_lite",
    "hilog:libhilog",
    "kv_store:datamgr_common",
    "runtime_core:ani_helpers",
  ]
}

generate_static_abc("clouddata_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.data.cloudData.ets" ]
  dst_file = "$target_out_dir/cloudData.abc"
  is_boot_abc = "True"
  device_dst_file = "/system/framework/cloudData.abc"
  dependencies = [ ":cloud_data_generate" ]
}

ohos_prebuilt_etc("clouddata_etc") {
  source = "$target_out_dir/cloudData.abc"
  module_install_dir = "framework"
  subsystem_name = "distributeddatamgr"
  part_name = "relational_store"
  deps = [ ":clouddata_abc" ]
}

group("clouddata_ani_pack") {
  deps = [
    ":cloud_data_taihe",
    ":clouddata_etc",
  ]
}
