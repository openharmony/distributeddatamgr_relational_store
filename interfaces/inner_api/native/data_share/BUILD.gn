# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import(
    "//foundation/distributeddatamgr/appdatamgr/frameworks/native/data_share/common/datashare.gni")

config("ability_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "common/include",
    "consumer/include",
    "provider/include",
    "${datashare_native_consumer_path}/include",
    "${datashare_native_provider_path}/include",
    "${aafwk_kits_path}/appkit/native/app/include",
    "${aafwk_services_path}/common/include",
    "${aafwk_kits_path}/ability/native/include/continuation/distributed",
    "${aafwk_kits_path}/ability/native/include/continuation/kits",
    "//foundation/aafwk/standard/interfaces/kits/napi/aafwk/inner/napi_common",
    "//foundation/distributeddatamgr/appdatamgr/interfaces/inner_api/native/rdb/include",
    "//foundation/distributeddatamgr/distributeddatamgr/frameworks/innerkitsimpl/rdb/include",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  defines = [
    "AMS_LOG_TAG = \"Ability\"",
    "AMS_LOG_DOMAIN = 0xD002200",
  ]
}

config("ability_public_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "common/include",
    "consumer/include",
    "provider/include",
    "${datashare_common_napi_path}/include",
    "${datashare_common_native_path}/include",
    "${datashare_native_consumer_path}/include",
    "${datashare_native_provider_path}/include",
    "//foundation/windowmanager/interfaces/innerkits/wm",
    "${aafwk_kits_path}/appkit/native/ability_runtime/app",
    "${aafwk_kits_path}/appkit/native/app/include",
    "${aafwk_kits_path}/appkit/native/ability_runtime/context",
  ]
}

ohos_shared_library("datashare_abilitykit") {
  include_dirs = []

  sources = [
    "${datashare_native_consumer_path}/src/datashare_connection.cpp",
    "${datashare_native_consumer_path}/src/datashare_helper.cpp",
    "${datashare_native_consumer_path}/src/datashare_proxy.cpp",
    "${datashare_native_provider_path}/src/datashare_ext_ability.cpp",
    "${datashare_native_provider_path}/src/datashare_ext_ability_context.cpp",
    "${datashare_native_provider_path}/src/datashare_stub.cpp",
    "${datashare_native_provider_path}/src/datashare_stub_impl.cpp",
    "${datashare_native_provider_path}/src/datashare_uv_queue.cpp",
    "${datashare_native_provider_path}/src/js_datashare_ext_ability.cpp",
    "${datashare_native_provider_path}/src/js_datashare_ext_ability_context.cpp",
  ]
  configs = [ ":ability_config" ]
  public_configs = [
    ":ability_public_config",
    "${aafwk_kits_path}/ability/ability_runtime:ability_context_public_config",
  ]

  deps = [
    "${aafwk_inner_api_path}/dataobs_manager:dataobs_manager",
    "${aafwk_kits_path}/ability/native:abilitykit_native",
    "${aafwk_kits_path}/appkit:app_context",
    "${datashare_common_native_path}:datashare_common",
  ]

  external_deps = [
    "ability_base:want",
    "ability_base:zuri",
    "ability_runtime:ability_context_native",
    "ability_runtime:ability_manager",
    "ability_runtime:app_manager",
    "ability_runtime:runtime",
    "ability_runtime:wantagent_innerkits",
    "access_token:libaccesstoken_sdk",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "dataability:native_dataability",
    "ipc:ipc_core",
    "ipc_js:rpc",
    "permission_standard:libpermissionsdk_standard",
    "relational_store:native_rdb",
    "utils_base:utils",
  ]

  public_deps = [
    "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog",
    "//foundation/arkui/napi:ace_napi",
  ]

  subsystem_name = "distributeddatamgr"
  part_name = "data_share"
}

ohos_shared_library("datashare_ext_ability_module") {
  include_dirs = [ "${datashare_native_provider_path}/include" ]

  sources = [ "${datashare_native_provider_path}/src/datashare_ext_ability_module_loader.cpp" ]

  configs = [ ":ability_config" ]
  public_configs = [ ":ability_public_config" ]

  deps = [
    ":datashare_abilitykit",
    "${aafwk_kits_path}/ability/native:abilitykit_native",
    "//foundation/bundlemanager/bundle_framework/common:libappexecfwk_common",
  ]

  external_deps = [
    "ability_base:want",
    "ability_runtime:runtime",
    "hiviewdfx_hilog_native:libhilog",
    "relational_store:native_appdatafwk",
    "utils_base:utils",
  ]

  subsystem_name = "distributeddatamgr"
  part_name = "data_share"
}
