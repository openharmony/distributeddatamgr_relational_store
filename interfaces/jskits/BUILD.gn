# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos/ace/ace.gni")

ohos_copy("appdatamgr_declaration") {
  sources = [ "./api" ]
  outputs = [ target_out_dir + "/$target_name/" ]
  module_source_dir = target_out_dir + "/$target_name"
  module_install_name = ""
}

ohos_shared_library("storage") {
  include_dirs = [
    "//third_party/node/src",
    "//utils/native/base/include",
    "../../frameworks/jskitsimpl/common",
  ]

  sources = [
    "../../frameworks/jskitsimpl/native_preferences/entry_point.cpp",
    "../../frameworks/jskitsimpl/native_preferences/napi_preference.cpp",
    "../../frameworks/jskitsimpl/native_preferences/napi_preference_helper.cpp",
  ]

  external_deps = [
    "hilog_native:libhilog",
    "napi:ace_napi",
    "native_appdatamgr:native_preferences",
  ]

  subsystem_name = "distributeddatamgr"
  part_name = "native_appdatamgr"
  relative_install_dir = "module/data"
}

ohos_shared_library("rdb") {
  include_dirs = [
    "//third_party/node/src",
    "//utils/native/base/include",
    "../../frameworks/jskitsimpl/common",
  ]

  sources = [
    "../../frameworks/jskitsimpl/common/js_ability.cpp",
    "../../frameworks/jskitsimpl/common/js_utils.cpp",
    "../../frameworks/jskitsimpl/native_rdb/entry_point.cpp",
    "../../frameworks/jskitsimpl/native_rdb/napi_rdb_predicates.cpp",
    "../../frameworks/jskitsimpl/native_rdb/napi_rdb_store.cpp",
    "../../frameworks/jskitsimpl/native_rdb/napi_rdb_store_helper.cpp",
    "../../frameworks/jskitsimpl/native_rdb/napi_result_set.cpp",
    "../../frameworks/jskitsimpl/native_rdb/napi_values_bucket.cpp",
  ]

  deps = [ "//utils/native/base:utils" ]

  external_deps = [
    "hilog_native:libhilog",
    "napi:ace_napi",
    "native_appdatamgr:native_appdatafwk",
    "native_appdatamgr:native_rdb",
  ]

  if (is_standard_system) {
    defines = [ "STANDARD_SYSTEM_ENABLE" ]

    deps += [ "//foundation/aafwk/standard/frameworks/kits/ability/native:abilitykit_native" ]

    external_deps += [
      "ability_runtime:ability_manager",
      "ability_runtime:app_manager",
      "ability_runtime:want",
      "bundle_framework:appexecfwk_base",
      "bundle_framework:appexecfwk_core",
    ]
  }

  subsystem_name = "distributeddatamgr"
  part_name = "native_appdatamgr"
  relative_install_dir = "module/data"
}

ohos_shared_library("dataability") {
  include_dirs = [
    "//third_party/node/src",
    "//utils/native/base/include",
    "../../frameworks/jskitsimpl/common",
    "../../frameworks/jskitsimpl/native_rdb",
  ]

  sources = [
    "../../frameworks/jskitsimpl/common/js_utils.cpp",
    "../../frameworks/jskitsimpl/native_dataability/entry_point.cpp",
    "../../frameworks/jskitsimpl/native_dataability/napi_data_ability_predicates.cpp",
    "../../frameworks/jskitsimpl/native_dataability/napi_predicates_utils.cpp",
  ]

  deps = [ "../../interfaces/jskits:rdb" ]

  external_deps = [
    "hilog_native:libhilog",
    "napi:ace_napi",
    "native_appdatamgr:native_dataability",
    "native_appdatamgr:native_rdb",
  ]

  subsystem_name = "distributeddatamgr"
  part_name = "native_appdatamgr"
  relative_install_dir = "module/data"
}
